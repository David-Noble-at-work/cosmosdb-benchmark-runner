plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

wrapper {
    gradleVersion = '5.1' //version required
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group = 'com.adobe.platform.core.identity.services'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
    }

}

project(':benchmark') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compile project(':async-client')
        compile project(':sync-client')
        compile project(':test-data-generator')
        
        compile "org.openjdk.jmh:jmh-core:1.21"
        annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.21"
    }

    jar { manifest { attributes "Main-Class": "org.openjdk.jmh.Main" } }

    shadowJar {
        classifier = 'shadow'
        append 'reference.conf'

    }

    task jmh(type: JavaExec, dependsOn: [':benchmark:clean', ':benchmark:shadowJar']) {
        main="-jar"
        doFirst {
            if (System.getProperty("jmhArgs")) {
                args System.getProperty("jmhArgs").split(',')
            }
            args = [shadowJar.archivePath, *args]
        }
    }

    javadoc { enabled = false }
}

project(':client-common'){
    dependencies {
        compile "com.typesafe:config:1.3.3"

        compile "org.slf4j:slf4j-api:1.7.6"
        compile "org.slf4j:slf4j-log4j12:1.7.6"
        compile "log4j:log4j:1.2.17"
    }
}

project(':async-client') {
    dependencies {
        compile project(':client-common')
        compile "com.microsoft.azure:azure-cosmosdb:2.6.1"
    }
}

project(':sync-client') {
    dependencies {
        compile project(':client-common')
        compile "com.microsoft.azure:azure-documentdb:2.4.0"
    }
}

project(':test-data-generator'){
    dependencies {
        compile project(':async-client')
    }
}